plugins {
    id 'fabric-loom' version '1.8-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}


repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }

    maven {
        url "https://maven.siphalor.de/"
    }

    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
    }

    //maven { url = "https://jitpack.io" }
    maven { url "https://maven.shedaniel.me/" }

    maven {
        url "https://maven.jamieswhiteshirt.com/libs-release"
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }

    maven { url 'https://maven.nucleoid.xyz' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://api.modrinth.com/maven' }

    maven {
        name 'Gegy'
        url 'https://maven.gegy.dev'
    }

    maven { url "https://api.modrinth.com/maven" }
    maven { url "https://server.bbkr.space/artifactory/libs-release" }  // LibGUI
    maven { url "https://mvn.devos.one/releases/" }
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    repositories {
        maven {
            url "https://maven.twelveiterations.com/repository/maven-public/"

            content {
                includeGroup "net.blay09.mods"
            }
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "curse.maven:travelers-backpack-fabric-541171:6105305"
    modImplementation "curse.maven:polymorph-388800:5736385"
    //REQUIRED

    //Cardinal Components
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_version}"

    //Cloth Config
    modRuntimeOnly("dev.architectury:architectury-fabric:${project.architectury_version}")
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"

    //Reach Entity Attributes
    include modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${project.reach_entity_attributes_version}")

    //OPTIONAL

    //Crafting Tweaks
    modImplementation "curse.maven:balm-fabric-500525:5644969"
    modImplementation "curse.maven:crafting-tweaks-fabric-502516:5623638"
    modApi("net.blay09.mods:kuma-api-fabric:[20.1.0,20.2.0)")

    //ModMenu
    modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"

    //Trinkets
    modImplementation("dev.emi:trinkets:${trinkets_version}")

    //Accessories
    modCompileOnly("maven.modrinth:accessories:TK44XAd7")
    modCompileOnly("maven.modrinth:accessories-tc-layer:byhR4pVP")

    //REI
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"

    //JEI
    modCompileOnly ("maven.modrinth:jei:NsbSea5x")

    //Comforts
    modCompileOnly ("maven.modrinth:comforts:D2F8i5te")

    //EMI
    modCompileOnly "dev.emi:emi-fabric:${project.emi_version}:api"

    //Dehydration
    modCompileOnly "curse.maven:dehydration-410830:4869849"

    //Universal Graves
    modCompileOnly "curse.maven:universal-graves-497175:4610073"

    //Carry On
    modCompileOnly "curse.maven:carryon-274259:4882498"

    //Yigd
    modCompileOnly("maven.modrinth:yigd:VslzbAWg")
    modCompileOnly("io.github.cottonmc:LibGui:8.1.1+1.20.1")
    modCompileOnly("me.lucko:fabric-permissions-api:0.2-SNAPSHOT")
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
